//! Covers src/core/cpu/conformance/base/long_shift_sat64_vectors.zig: each
//! vector runs through the `long_shift_sat64` group on a bare core holding
//! N, C and the vector's Q.
const std = @import("std");
const ra8 = @import("ra8");
const vector = ra8.core.conformance_vector;
const vectors = ra8.core.conformance_suite.base.long_shift_sat64_vectors;
const cpu_ns = ra8.core.cpu;
const Cpu = cpu_ns.cpu.Cpu;
const bits = cpu_ns.regs.xpsr_bits;
const nzcvq: u32 = 0xF800_0000;

fn put(cpu: *Cpu, r: u4, value: u32) void {
    if (r != 13 and r != 15) cpu.regs.set(r, value);
}

fn get(cpu: *const Cpu, r: u4) u32 {
    return if (r == 13 or r == 15) 0 else cpu.regs.get(r);
}

fn run(in: vectors.In) vectors.Out {
    const q: u32 = if (in.q) bits.q else 0;
    var cpu: Cpu = .{ .bus = undefined, .regs = .{ .xpsr = bits.thumb | vectors.flags | q } };
    const lo: u4 = @intCast(in.hw1 & 0xE);
    const hi: u4 = @intCast((in.hw2 >> 8) & 0xF);
    put(&cpu, lo, @truncate(in.a));
    put(&cpu, hi, @truncate(in.a >> 32));
    if (in.hw2 & 0xF == 0xD) put(&cpu, @intCast(in.hw2 >> 12), in.rm);
    const instr: cpu_ns.instr.Instr = .{ .address = 0x1000, .hw1 = in.hw1, .hw2 = in.hw2, .size = @intCast(in.size) };
    const exec = cpu_ns.ops.long_shift_sat64.group.decode(instr) orelse return .{ .claimed = false, .flags = 0 };
    exec(&cpu, instr) catch return .{ .claimed = false, .rd = 1, .flags = 0 };
    const rd = (@as(u64, get(&cpu, hi)) << 32) | get(&cpu, lo);
    return .{ .rd = rd, .flags = cpu.regs.xpsr & nzcvq };
}

test "long_shift_sat64 matches the Arm ARM" {
    try vector.expectAll(vectors.In, vectors.Out, run, &vectors.all);
}

test "every vector is counted in covered and names the group" {
    try std.testing.expectEqual(vectors.all.len, vectors.covered.len);
    for (vectors.covered) |name| try std.testing.expectEqualStrings("long_shift_sat64", name);
}
