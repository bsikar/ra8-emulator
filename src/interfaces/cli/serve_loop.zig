//! The answer loop the serve front runs on one connected peer (RA8EMU-736).
const std = @import("std");
const builtin = @import("builtin");
const served = @import("../rpc/session_server.zig");
const Stdio = @import("../rpc/stdio_transport.zig").Stdio;
const socket_flags = @import("../socket_flags.zig");
const sock_ready = @import("../sock_ready.zig");
const win32 = @import("../win32.zig");

/// How long an idle server waits on its peer before checking again.
const idle_wait_ms = 20;

pub const Buffers = struct { rx: []u8, tx: []u8 };

/// What the peer's descriptor is, which decides how a hang-up shows.
pub const Link = enum { pipe, socket };

/// Set from a signal handler; the loops end at their next idle check.
pub var stopping = std.atomic.Value(bool).init(false);

/// Answer the client on stdin and stdout until stdin closes.
pub fn answerStdio(context: *served.Context, buffers: Buffers) !void {
    var stdio = Stdio.process();
    try answer(context, stdio.transport(), stdio.input, .pipe, buffers);
}

/// Answer frames on `wire` until the peer behind `fd` closes or a stop is asked.
pub fn answer(context: *served.Context, wire: served.rpc_lib.Transport, fd: std.posix.fd_t, link: Link, buffers: Buffers) !void {
    var host = served.Host.init(wire, buffers.rx, context);
    while (true) {
        if (try host.poll(buffers.tx) != .idle) continue;
        if (stopping.load(.acquire) or try closed(fd, link)) return;
    }
}

/// Wait briefly on `fd`; true once the peer hung up with nothing left to read.
/// A pipe reports that as HUP. A socket reports it as readable with zero
/// bytes behind it, so the socket is peeked.
fn closed(fd: std.posix.fd_t, link: Link) !bool {
    if (on_windows and link == .pipe) return pipeClosed(fd);
    const ready = try sock_ready.wait(fd, idle_wait_ms);
    if (!ready.readable) return ready.hung_up;
    if (link == .pipe) return false;
    return peerGone(fd);
}

const on_windows = builtin.os.tag == .windows;

/// Windows pipes have no wait; an empty pipe sleeps the idle wait instead.
fn pipeClosed(pipe: std.posix.fd_t) bool {
    const waiting = win32.pipeWaiting(pipe) orelse return true;
    if (waiting == 0) win32.Sleep(idle_wait_ms);
    return false;
}

/// Peek one byte: zero bytes means the peer closed its end.
fn peerGone(fd: std.posix.fd_t) bool {
    var byte: [1]u8 = undefined;
    if (on_windows) {
        const got = win32.recv(fd, &byte, byte.len, win32.msg_peek);
        if (got < 0) return win32.WSAGetLastError() != win32.wsa_would_block;
        return got == 0;
    }
    const flags = socket_flags.peek | socket_flags.dontwait;
    const got = std.c.recv(fd, &byte, byte.len, @intCast(flags));
    if (got < 0) return std.c.errno(got) != .AGAIN;
    return got == 0;
}
